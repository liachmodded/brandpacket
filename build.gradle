plugins {
    id 'fabric-loom' version '0.12-SNAPSHOT'
    id 'maven-publish'
    id 'org.cadixdev.licenser' version '0.6.1'
}

def ENV = System.getenv()
version = project.modVersion + '.' + ENV.BUILD_NUMBER ?: "adhoc"

group = project.mavenGroup

sourceSets {
    //modmenu {
    //    compileClasspath += sourceSets.main.output
    //}
}

dependencies {
    //to change the versions see the gradle.properties file
    minecraft "com.mojang:minecraft:${project.minecraftVersion}"
    mappings "net.fabricmc:yarn:${project.yarnMappings}"
    modImplementation "net.fabricmc:fabric-loader:${project.loaderVersion}"
    
    //modImplementation "io.github.prospector:modmenu:${project.modmenuVersion}"
}

license {
    header = file('HEADER.txt')
    include '**/*.java'
    style.java = 'BLOCK_COMMENT'
    newLine = false
}

tasks.withType(JavaCompile) {
    options.release = 17
    options.encoding = "UTF-8"
}

processResources {
    inputs.properties "version": project.version

    filesMatching('fabric.mod.json') {
        expand "version": project.version
    }
}

java {
    withSourcesJar()
}

tasks.withType(Jar) {
    from "LICENSE"
}

// configure the maven publication
publishing {
    publications {
        mavenJava(MavenPublication) {
            artifact(remapJar) {
                builtBy remapJar
            }
            artifact(sourcesJar) {
                builtBy remapSourcesJar
            }
        }
    }

    repositories {
        if (ENV.GITHUB_TOKEN) {
            maven {
                name = "GitHub"
                url = uri("https://maven.pkg.github.com/liachmodded/brandpacket")
                credentials {
                    username = ENV.GITHUB_ACTOR
                    password = ENV.GITHUB_TOKEN
                }
            }
        }
    }
}
